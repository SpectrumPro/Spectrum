# Copyright (c) 2024 Liam Sherwin, All rights reserved.
# This file is part of the Spectrum Lighting Engine, licensed under the GPL v3.

class_name UISettingsInputActions extends PanelContainer
## Input Action settings.


## ItemList for inputactions
@export var _input_action_tree: Tree

## The RemoveInputAction button
@export var _remove_action_button: Button

## The InputActionSettingsComponent
@export var _input_action_settings: InputActionSettingsComponent


## All current input action
var _input_actions: RefMap = RefMap.new()


## Loads the input items into the list
func _ready() -> void:
	_input_action_tree.create_item()
	
	InputServer.input_action_added.connect(_add_input_action)
	InputServer.input_action_removed.connect(_remove_input_action)
	
	for action: InputAction in InputServer.get_input_actions():
		_add_input_action(action)


## Adds an InputAction to the list
func _add_input_action(p_action: InputAction) -> void:
	var tree_item: TreeItem = _input_action_tree.create_item()
	
	p_action.name_changed.connect(func (new_name: String):
		tree_item.set_text(0, new_name)
	)
	
	tree_item.set_text(0, p_action.get_name())
	_input_actions.map(p_action, tree_item)


## Removes an InputAction to the list
func _remove_input_action(p_action: InputAction) -> void:
	_input_actions.left(p_action).free()
	_input_actions.erase_left(p_action)
	_remove_action_button.set_disabled(true)
	_input_action_settings.set_input_action(null)


## Called when the AddInputAction button is pressed
func _on_add_input_action_pressed() -> void:
	InputServer.create_input_action()


## Called when the RemoveInputAction button is pressed
func _on_remove_input_action_pressed() -> void:
	var tree_item: TreeItem = _input_action_tree.get_selected()
	InputServer.remove_input_action(_input_actions.right(tree_item))


## Called when an item is selected in the ItemList
func _on_item_list_item_selected() -> void:
	_remove_action_button.set_disabled(false)
	_input_action_settings.set_input_action(_input_actions.right(_input_action_tree.get_selected()))


## Called when nothing is selected in the ItemList
func _on_item_list_empty_clicked(at_position: Vector2, mouse_button_index: int) -> void:
	_input_action_tree.deselect_all()
	_remove_action_button.set_disabled(true)
	_input_action_settings.set_input_action(null)
	
